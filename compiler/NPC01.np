MODULE NPC01;

IMPORT Sys, Buf, Base := NPC01B, Scanner := NPC01S;


CONST

  MaxTypes = 20; MaxSymbols = 600;
  Marker = MaxSymbols - 1;

  ClassScope = 1; ClassModule = 2; ClassQual = 3; ClassConst = 4;
  ClassType = 5; ClassVar = 6; ClassProc = 7; ClassField = 8; ClassFunc = 9;

  ModeReg = 10; ModeCond = 11;

  FormBool = 1; FormChar = 2; FormInt = 3; FormArray = 8; FormRecord = 9;

  RegGP = 11; (* global pointer *)
  RegFP = 12; (* frame pointer *)
  RegSP = 13; (* stack pointer *)
  RegRT = 14; (* return register *)
  RegPC = 15; (* program counter *)

  OcMOV = 8; OcMVN = 9; OcCMP = 10; OcMOVI = 16; OcCMPI = 18; 
  OcMUL = 11; OcDIV = 12; OcMOD = 13; OcADD = 14; OcSUB = 15;
  OcMULI =19; OcADDI = 22; 
  OcLDB = 24; OcLDW = 25; OcSTB = 26; OcSTW = 27; OcPOP = 28; OcPSH = 29;
  OcSYS = 31; OcJUMP = 48; OcCALL = 49;
  OcBEQ = 50; OcBNE = 51; OcBLS = 52; OcBGE = 53; OcBLE = 54; OcBGT = 55;


TYPE

  Type = RECORD
    form : INTEGER;
    size : INTEGER;
    len : INTEGER;
    value : INTEGER; (* - array: base type
                        - record: scope *)
    link: INTEGER (* - owner symbol (allocated) or
                     - next in free list (deallocated),
                     - -1 = NIL *)
  END;

  Symbol = RECORD
    name : Scanner.Ident;
    public : BOOLEAN;
    level : INTEGER;
    class : INTEGER;
    type : INTEGER;
    scope : INTEGER; (* parent scope *)
    value : INTEGER; (* - const or address
                        - ClassModule 0 if not yet compiled
                        - ClassQual import, ref to module *)
    value2 : INTEGER; (* - deref level for VAR params or
                         - forward address for ClassProc
                         - next module *)
    next: INTEGER (* - next in scope (allocated) or
                     - free list (deallocated),
                     - -1 = NIL *)
  END;

  Gen = RECORD
    mode : INTEGER;
    type : INTEGER;
    size : INTEGER;
    level : INTEGER;
    reg : INTEGER;
    addr1 : INTEGER;
    addr2 : INTEGER;
    cond : INTEGER;
    expConst : BOOLEAN
  END;


VAR

  code : ARRAY 10000 OF INTEGER;
  source : Scanner.Source; (* root module *)
  types : ARRAY MaxTypes OF Type;
  freeTypes : INTEGER; (* first type of free type list *)
  symbols : ARRAY MaxSymbols OF Symbol;
  freeSymbols : INTEGER; (* first symbol of free symbol list *)
  allocSymbol : INTEGER; (* last allocated symbol *)
  topScope : INTEGER; (* top symbol of scope stack *)
  modules : INTEGER; (* linked list of module symbols *)
  level : INTEGER; (* level of nested procedures *)
  boolType : INTEGER; charType : INTEGER; intType : INTEGER;
  varsize : INTEGER;
  regs : ARRAY RegGP OF BOOLEAN;
  pc : INTEGER; (* program counter *)
  entrypoint : INTEGER;


(* symbol tables *)


PROCEDURE STableInitialize;

  VAR i : INTEGER;

  BEGIN
    i := 0;
    WHILE i < MaxTypes DO
      types[i].form := 0;
      types[i].size := 0;
      types[i].len := 0;
      types[i].value := -1;
      types[i].link := i + 1;
      i := i + 1
    END;
    types[i - 1].link := -1;
    freeTypes := 0;
    i := 0;
    WHILE i < MaxSymbols DO
      symbols[i].name.len := 0;
      symbols[i].level := 0;
      symbols[i].class := 0;
      symbols[i].type := -1;
      symbols[i].scope := -1;
      symbols[i].value := 0;
      symbols[i].value2 := 0;
      symbols[i].next := i + 1;
      i := i + 1
    END;
    symbols[Marker].next := -1;
    freeSymbols := 0;
    topScope := -1
  END STableInitialize;


PROCEDURE STableAllocType(VAR s : Scanner.Source; VAR t : INTEGER;
                          form : INTEGER; size: INTEGER; link : INTEGER);

  BEGIN
    IF freeTypes # -1 THEN
      types[freeTypes].form := form;
      types[freeTypes].size := size;
      types[freeTypes].len := 0;
      types[freeTypes].value := -1;
      t := freeTypes;
      freeTypes := types[t].link;
      types[t].link := link
    ELSE
      Scanner.RaiseError(s, Base.ErrOutOfMemTypes)
    END
  END STableAllocType;


PROCEDURE STableCompIdent(VAR eq : BOOLEAN;
                          VAR i1 : Scanner.Ident;
                          VAR i2 : Scanner.Ident);

  VAR i : INTEGER;

  BEGIN
    IF i1.len # i2.len THEN
      eq := FALSE
    ELSE
      i := 0; eq := TRUE;
      WHILE i < i1.len DO
        IF i1.data[i] = i2.data[i] THEN
          i := i + 1
        ELSE
          i := i1.len;
          eq := FALSE
        END
      END
    END
  END STableCompIdent;


PROCEDURE STableCopyIdent(VAR trg : Scanner.Ident; VAR src : Scanner.Ident);

  VAR i : INTEGER;

  BEGIN
    trg.len := src.len; i := 0;
    WHILE i < src.len DO
      trg.data[i] := src.data[i]; i := i + 1
    END
  END STableCopyIdent;


PROCEDURE STableAllocSymbol(VAR s : Scanner.Source; class : INTEGER);

  BEGIN
    IF freeSymbols # Marker THEN
      IF class = ClassScope THEN
        symbols[freeSymbols].name.len := 0;
        symbols[freeSymbols].scope := topScope;
        topScope := freeSymbols
      ELSE
        STableCopyIdent(symbols[freeSymbols].name, symbols[Marker].name);
        symbols[freeSymbols].scope := -1
      END;
      symbols[freeSymbols].public := FALSE;
      symbols[freeSymbols].level := level;
      symbols[freeSymbols].class := class;
      symbols[freeSymbols].type := -1;
      symbols[freeSymbols].value := 0;
      symbols[freeSymbols].value2 := 0;
      allocSymbol := freeSymbols;
      freeSymbols := symbols[allocSymbol].next;
      symbols[allocSymbol].next := Marker
    ELSE
      Scanner.RaiseError(s, Base.ErrOutOfMemSymbols)
    END
  END STableAllocSymbol;


PROCEDURE STableFindSymbol(VAR y : INTEGER; scope : INTEGER);

  VAR eq : BOOLEAN;

  BEGIN
    y := scope;
    STableCompIdent(eq,
      symbols[symbols[y].next].name, symbols[Marker].name);
    WHILE ~eq DO
      y := symbols[y].next;
      STableCompIdent(eq,
        symbols[symbols[y].next].name, symbols[Marker].name)
    END
  END STableFindSymbol;


PROCEDURE STableDeallocScope(scope : INTEGER; keepPublics : BOOLEAN);

  VAR y : INTEGER; yNext : INTEGER; yPrev : INTEGER;


  PROCEDURE symbol(yIx : INTEGER; VAR y : Symbol; keepPublics : BOOLEAN);


    PROCEDURE type(t : INTEGER; keepPublics : BOOLEAN);

      BEGIN
        IF ~keepPublics & (types[t].form = FormArray) THEN
          IF types[types[t].value].link = -1 THEN
            type(types[t].value, keepPublics)
          END;
          types[t].link := freeTypes; freeTypes := t
        ELSIF types[t].form = FormRecord THEN
          STableDeallocScope(types[t].value, keepPublics);
          IF ~keepPublics THEN
            types[t].link := freeTypes; freeTypes := t
          END
        END
      END type;


    BEGIN (* symbol *)
      IF (y.type >= 0) &
         ((types[y.type].link = yIx) OR (types[y.type].link = -1)) THEN
        type(y.type, keepPublics)
      END;
      IF ~keepPublics & (y.class = ClassProc) THEN
        STableDeallocScope(y.scope, FALSE)
      END
    END symbol;


  BEGIN (* STableDeallocScope *)
    y := scope; yPrev := -1;
    WHILE y # Marker DO
      symbol(y, symbols[y], keepPublics);
      yNext := symbols[y].next;
      IF keepPublics &
         ((symbols[y].class = ClassScope) OR symbols[y].public) THEN
        yPrev := y; y := yNext
      ELSE
        IF yPrev # -1 THEN
          symbols[yPrev].next := yNext
        END;
        symbols[y].next := freeSymbols;
        freeSymbols := y;
        y := yNext
      END
    END
  END STableDeallocScope;


PROCEDURE STableInitUniverse;

  VAR y : INTEGER;


  PROCEDURE enter(VAR s : Scanner.Source; VAR y : INTEGER; len : INTEGER;
                  chars1 : INTEGER; chars2 : INTEGER;
                  class : INTEGER; value : INTEGER; type : INTEGER);


    PROCEDURE enterChars(ix : INTEGER; chars : INTEGER);

      BEGIN
        IF chars > 0 THEN
          enterChars(ix + 1, chars DIV 256);
          symbols[Marker].name.data[ix] := CHR(chars MOD 256)
        END
      END enterChars;


    BEGIN (* enter *)
      enterChars(0, chars1); enterChars(4, chars2);
      symbols[Marker].name.len := len;
      STableAllocSymbol(s, class);
      symbols[y].next := allocSymbol;
      y := allocSymbol;
      symbols[y].value := value;
      symbols[y].type := type
    END enter;


  BEGIN (* STableInitUniverse *)
    STableAllocType(source, boolType, FormBool, 1, -1);
    STableAllocType(source, charType, FormChar, 1, -1);
    STableAllocType(source, intType, FormInt, 4, -1);
    y := topScope;
    enter(source, y, 7, 4C4F4F42H, 4E4145H, ClassType, 0, boolType); (* BOOLEAN*)
    enter(source, y, 4, 52414843H, 0, ClassType, 0, charType); (* CHAR *)
    enter(source, y, 7, 45544E49H, 524547H, ClassType, 0, intType); (* INTEGER *)
    enter(source, y, 4, 52444441H, 0, ClassFunc, 1, intType); (* ADDR *)
    enter(source, y, 3, 524843H, 0, ClassFunc, 2, charType); (* CHR *)
    enter(source, y, 3, 44524FH, 0, ClassFunc, 3, intType)  (* ORD *)
  END STableInitUniverse;


(* generator *)


PROCEDURE GenInitRegs;

  VAR i : INTEGER;

  BEGIN
    i := 0;
    WHILE i < RegGP DO
      regs[i] := FALSE; i := i + 1
    END
  END GenInitRegs;


PROCEDURE GenAllocReg(VAR s : Scanner.Source; VAR r : INTEGER);

  VAR r2 : INTEGER;

  BEGIN
    r := -1; r2 := 0;
    WHILE r2 < RegGP DO
      IF regs[r2] THEN
        r2 := r2 + 1
      ELSE
        r := r2;
        regs[r] := TRUE;
        r2 := RegGP
      END
    END;
    IF r = -1 THEN
      Scanner.RaiseError(s, Base.ErrOutOfRegs)
    END
  END GenAllocReg;


PROCEDURE GenFreeReg(r : INTEGER);

  BEGIN
    IF r < RegGP THEN
      regs[r] := FALSE
    END
  END GenFreeReg;


PROCEDURE GenCheckRegs(VAR s : Scanner.Source);

  VAR r : INTEGER;

  BEGIN
    r := 0;
    WHILE r < RegGP DO
      IF regs[r] THEN
        Scanner.RaiseError(s, Base.ErrCheckRegs)
      END;
      r := r + 1
    END
  END GenCheckRegs;


PROCEDURE GenAlign(VAR size : INTEGER);

  VAR i : INTEGER; j : INTEGER;

  BEGIN
    IF size < 0 THEN i := -4 ELSE i := 4 END;
    j := size MOD i;
    IF j # 0 THEN
      size := size + (i - j)
    END
  END GenAlign;


PROCEDURE GenEmit(instr : INTEGER);

  BEGIN
    code[pc] := instr; pc := pc + 1
  END GenEmit;


PROCEDURE GenEmitOc1(oc : INTEGER; c : INTEGER);

  BEGIN
    GenEmit(oc + c * 64)
  END GenEmitOc1;


PROCEDURE GenEmitOc3(oc : INTEGER; a : INTEGER; b : INTEGER; c : INTEGER);

  BEGIN
    IF (c >= -20000H) & (c < 20000H) THEN
      GenEmit(oc + a * 64 + b * 1024 + c * 16384)
    ELSE
      GenEmit(oc + 16 + a * 64 + b * 1024);
      GenEmit(c)
    END
  END GenEmitOc3;


PROCEDURE GenFixLink(l : INTEGER);

  VAR l2 : INTEGER;

  BEGIN
    WHILE l # 0 DO
      l2 := code[l];
      code[l] := l2 MOD 64 + 64 * (pc - l - 1);
      l := l2 DIV 64
    END
  END GenFixLink;


PROCEDURE GenMergeLinks(VAR l : INTEGER ; l2 : INTEGER);

  VAR l3 : INTEGER; l4 : INTEGER; l5 : INTEGER; continue : BOOLEAN;

  BEGIN
    IF l2 # 0 THEN
      l3 := l2;
      continue := TRUE;
      WHILE continue DO
        code[l3] := l4;
        l5 := l4 DIV 64;
        IF l5 = 0 THEN
          continue := FALSE
        ELSE
          l3 := l5
        END
      END;
      code[l3] := l4 + 64 * l;
      l := l2
    END
  END GenMergeLinks;


PROCEDURE GenNegCond(VAR c : INTEGER);

  BEGIN
    IF c = Scanner.TokEq THEN c := Scanner.TokEqN
    ELSIF c = Scanner.TokEqN THEN c := Scanner.TokEq
    ELSIF c = Scanner.TokLss THEN c := Scanner.TokGEq
    ELSIF c = Scanner.TokGEq THEN c := Scanner.TokLss
    ELSIF c = Scanner.TokLEq THEN c := Scanner.TokGtr
    ELSIF c = Scanner.TokGtr THEN c := Scanner.TokLEq
    END
  END GenNegCond;


PROCEDURE GenEmitCondBR(cond : INTEGER; disp : INTEGER; negated : BOOLEAN);

  VAR oc : INTEGER;

  BEGIN
    IF negated THEN GenNegCond(cond) END;
    IF cond = Scanner.TokEq THEN oc := OcBEQ
    ELSIF cond = Scanner.TokEqN THEN oc := OcBNE
    ELSIF cond = Scanner.TokLss THEN oc := OcBLS
    ELSIF cond = Scanner.TokGEq THEN oc := OcBGE
    ELSIF cond = Scanner.TokLEq THEN oc := OcBLE
    ELSIF cond = Scanner.TokGtr THEN oc := OcBGT
    END;
    GenEmitOc1(oc, disp)
  END GenEmitCondBR;


PROCEDURE GenBCall(l : INTEGER);

  BEGIN
    GenEmitOc1(OcCALL, -l - pc - 1)
  END GenBCall;


PROCEDURE GenFCall(VAR l : INTEGER);

  BEGIN
    GenEmitOc1(OcCALL, -l);
    l := -(pc - 1)
  END GenFCall;


PROCEDURE GenBJump(l : INTEGER);

  BEGIN
    GenEmitOc1(OcJUMP, l - pc - 1)
  END GenBJump;


PROCEDURE GenFJump(VAR l : INTEGER);

  BEGIN
    GenEmitOc1(OcJUMP, l);
    l := pc - 1
  END GenFJump;


PROCEDURE GenEnter(varsize : INTEGER);

  BEGIN
    GenEmitOc3(OcPSH, RegRT, RegSP, -4);
    GenEmitOc3(OcPSH, RegFP, RegSP, -4);
    GenEmitOc3(OcMOV, RegFP, 0, RegSP);
    IF varsize # 0 THEN
      GenEmitOc3(OcADDI, RegSP, RegSP, varsize)
    END
  END GenEnter;


PROCEDURE GenReturn(parsize : INTEGER);

  BEGIN
    GenEmitOc3(OcMOV, RegSP, 0, RegFP);
    GenEmitOc3(OcPOP, RegFP, RegSP, 4);
    GenEmitOc3(OcPOP, RegPC, RegSP, 4 + parsize)
  END GenReturn;


PROCEDURE GenMake(VAR s : Scanner.Source; VAR g : Gen; VAR y : Symbol);

  VAR r : INTEGER;

  BEGIN
    g.mode := y.class;
    g.type := y.type;
    g.size := types[y.type].size;
    g.level := y.level;
    g.addr1 := y.value;
    g.addr2 := y.value2;
    IF g.level = 0 THEN
      g.reg := RegGP
    ELSE
      g.reg := RegFP
    END;
    WHILE g.addr2 > 0 DO
      GenAllocReg(s, r);
      GenEmitOc3(OcLDW, r, g.reg, g.addr1);
      GenFreeReg(g.reg);
      g.reg := r;
      g.addr1 := 0; (* --> GenParam, GenAddr *)
      g.addr2 := g.addr2 - 1
    END
  END GenMake;


PROCEDURE GenLoad(VAR s : Scanner.Source; VAR g : Gen);

  VAR r : INTEGER;

  BEGIN
    IF g.mode # ModeReg THEN
      IF g.mode = ClassConst THEN
        GenAllocReg(s, g.reg);
        GenEmitOc3(OcMOVI, g.reg, 0, g.addr1)
      ELSIF g.mode = ClassVar THEN
        GenAllocReg(s, r);
        IF g.size = 1 THEN
          GenEmitOc3(OcLDB, r, g.reg, g.addr1)
        ELSE
          GenEmitOc3(OcLDW, r, g.reg, g.addr1)
        END;
        GenFreeReg(g.reg);
        g.reg := r
      ELSIF g.mode = ModeCond THEN
        GenEmitCondBR(g.cond, 2, TRUE);
        GenFreeReg(g.reg);
        GenFixLink(g.addr2);
        GenAllocReg(s, g.reg);
        GenEmitOc3(OcMOVI, g.reg, 0, 1);
        GenEmitOc1(OcJUMP, 1);
        GenFixLink(g.addr1);
        GenEmitOc3(OcMOVI, g.reg, 0, 0)
      END;
      g.mode := ModeReg
    END
  END GenLoad;


PROCEDURE GenLoadCond(VAR s : Scanner.Source; VAR g : Gen);

  BEGIN
    IF g.mode # ModeCond THEN
      GenLoad(s, g);
      GenEmitOc3(OcCMPI, g.reg, g.reg, 0);
      GenFreeReg(g.reg); GenCheckRegs(s);
      g.mode := ModeCond;
      g.addr1 := 0;
      g.addr2 := 0;
      g.cond := Scanner.TokEqN
    END
  END GenLoadCond;


PROCEDURE GenCondFJump(VAR s : Scanner.Source; VAR g : Gen);

  BEGIN
    GenLoadCond(s, g);
    GenEmitCondBR(g.cond, g.addr1, TRUE);
    GenFreeReg(g.reg);
    GenFixLink(g.addr2);
    g.addr1 := pc - 1;
    GenCheckRegs(s)
  END GenCondFJump;


PROCEDURE GenStore(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

  BEGIN
    GenLoad(s, g2);
    IF g.size = 1 THEN
      GenEmitOc3(OcSTB, g2.reg, g.reg, g.addr1)
    ELSE
      GenEmitOc3(OcSTW, g2.reg, g.reg, g.addr1)
    END;
    GenFreeReg(g2.reg); GenFreeReg(g.reg); GenCheckRegs(s)
  END GenStore;


PROCEDURE GenParam(VAR s : Scanner.Source; VAR g : Gen; isRef : BOOLEAN);

  VAR r : INTEGER;

  BEGIN
    IF isRef THEN (* ref param *)
      IF g.mode = ClassVar THEN
        IF g.addr1 = 0 (* -->GenMake *) THEN
          r := g.reg
        ELSE
          GenAllocReg(s, r);
          GenEmitOc3(OcADDI, r, g.reg, g.addr1);
          GenFreeReg(g.reg)
        END;
        GenEmitOc3(OcPSH, r, RegSP, -4);
        GenFreeReg(r)
      ELSE
        Scanner.RaiseError(s, Base.ErrNotLValue)
      END
    ELSE (* value param *)
      GenLoad(s, g);
      GenEmitOc3(OcPSH, g.reg, RegSP, -4)
    END;
    GenFreeReg(g.reg); GenCheckRegs(s)
  END GenParam;


PROCEDURE GenField(VAR g : Gen; VAR y : Symbol);

  BEGIN
    g.addr1 := g.addr1 + y.value
  END GenField;


PROCEDURE GenIndex(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

  VAR size : INTEGER;

  BEGIN
    size := types[types[g.type].value].size;
    IF g2.mode = ClassConst THEN
      IF (g2.addr1 >= 0) &
         (g2.addr1 < types[g.type].len) THEN
        g.addr1 := g.addr1 + g2.addr1 * size
      ELSE
        Scanner.RaiseError(s, Base.ErrBadIndex)
      END
    ELSE
      GenLoad(s, g2);
      IF size > 1 THEN
        GenEmitOc3(OcMULI, g2.reg, g2.reg, size)
      END;
      GenEmitOc3(OcADD, g2.reg, g2.reg, g.reg);
      GenFreeReg(g.reg);
      g.reg := g2.reg
    END
  END GenIndex;


PROCEDURE GenOp(VAR s : Scanner.Source; op : INTEGER; VAR g : Gen; VAR g2 : Gen);

  BEGIN
    GenLoad(s, g);
    IF g2.mode = ClassConst THEN
      GenEmitOc3(op + 8, g.reg, g.reg, g2.addr1)
    ELSE
      GenLoad(s, g2);
      GenEmitOc3(op, g.reg, g.reg, g2.reg);
      GenFreeReg(g2.reg)
    END
  END GenOp;


PROCEDURE GenOp1(VAR s : Scanner.Source; VAR g : Gen; op : INTEGER);

  VAR l : INTEGER;

  BEGIN
    IF op = Scanner.TokMinus THEN
      IF g.mode = ClassVar THEN
        GenLoad(s, g)
      END;
      GenEmitOc3(OcMVN, g.reg, 0, g.reg)
    ELSIF op = Scanner.TokTilde THEN
      IF g.mode = ClassConst THEN
        IF g.addr1 = 1 THEN g.addr1 := 0 ELSE g.addr1 := 1 END
      ELSE
        GenLoadCond(s, g); GenNegCond(g.cond);
        l := g.addr1; g.addr1 := g.addr2; g.addr2 := l
      END
    ELSIF op = Scanner.TokAmper THEN
      GenLoadCond(s, g);
      GenEmitCondBR(g.cond, g.addr1, TRUE);
      GenFreeReg(g.reg);
      g.addr1 := pc - 1;
      GenFixLink(g.addr2);
      g.addr2 := 0
    ELSIF op = Scanner.TokOr THEN
      GenLoadCond(s, g);
      GenEmitCondBR(g.cond, g.addr2, FALSE);
      GenFreeReg(g.reg);
      g.addr2 := pc - 1;
      GenFixLink(g.addr1);
      g.addr1 := 0
    END
  END GenOp1;


PROCEDURE GenOp2(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen; op : INTEGER);

  BEGIN
    IF types[g.type].form = FormInt THEN
      IF op = Scanner.TokAsterisk THEN GenOp(s, OcMUL, g, g2)
      ELSIF op = Scanner.TokDiv THEN GenOp(s, OcDIV, g, g2)
      ELSIF op = Scanner.TokMod THEN GenOp(s, OcMOD, g, g2)
      ELSIF op = Scanner.TokPlus THEN GenOp(s, OcADD, g, g2)
      ELSIF op = Scanner.TokMinus THEN GenOp(s, OcSUB, g, g2)
      END
    ELSE (* FormBool *)
      IF (g.mode = ClassConst) & (g2.mode = ClassConst) THEN
        IF op = Scanner.TokOr THEN
          IF (g.addr1 = 1) OR (g2.addr1 = 1) THEN
            g.addr1 := 1
          ELSE
            g.addr1 := 0
          END
        ELSE (* Scanner.TokAmper *)
          IF (g.addr1 = 1) & (g2.addr1 = 1) THEN
            g.addr1 := 1
          ELSE
            g.addr1 := 0
          END
        END
      ELSE
        GenLoadCond(s, g2);
        IF op = Scanner.TokOr THEN
          g.addr1 := g2.addr1;
          GenMergeLinks(g.addr2, g2.addr2)
        ELSE (* Scanner.TokAmper *)
          GenMergeLinks(g.addr1, g2.addr1);
          g.addr2 := g2.addr2
        END;
        g.cond := g2.cond
      END
    END
  END GenOp2;


PROCEDURE GenRel(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen; op : INTEGER);

  BEGIN
    GenOp(s, OcCMP, g, g2);
    g.cond := op;
    g.mode := ModeCond;
    g.addr1 := 0;
    g.addr2 := 0;
    GenFreeReg(g.reg); GenCheckRegs(s)
  END GenRel;


PROCEDURE GenSys(VAR s : Scanner.Source; call : INTEGER; VAR g : Gen; VAR g2 : Gen);

  VAR r : INTEGER;

  BEGIN
    GenLoad(s, g2);
    GenAllocReg(s, r);
    GenEmitOc3(OcSYS, r, g2.reg, call);
    GenEmitOc3(OcSTW, r, g.reg, g.addr1);
    GenFreeReg(r);
    GenFreeReg(g.reg);
    GenFreeReg(g2.reg)
  END GenSys;


PROCEDURE GenAddr(VAR s : Scanner.Source; VAR g : Gen);

  VAR r : INTEGER;

  BEGIN
    IF g.mode = ClassVar THEN
      IF g.addr1 # 0 (* -->GenMake *) THEN
        GenAllocReg(s, r);
        GenEmitOc3(OcADDI, r, g.reg, g.addr1);
        GenFreeReg(g.reg);
        g.reg := r
      END;
      g.mode := ModeReg
    ELSE
      Scanner.RaiseError(s, Base.ErrNotLValue)
    END
  END GenAddr;


PROCEDURE GenCreateFile;

  CONST FileOpenModeWrite = 2;

  VAR filename : INTEGER; file : INTEGER; n : INTEGER;

  BEGIN
    Sys.GetArg(filename, 1);
    Sys.OpenFile(file, filename, FileOpenModeWrite);
    Sys.WriteFile(n, file, ADDR(code), pc * 4);
    Sys.CloseFile(file)
  END GenCreateFile;


(* parser *)


PROCEDURE parse;

  VAR yModule : INTEGER;


  PROCEDURE accept(VAR s : Scanner.Source; token : INTEGER);

    BEGIN
      IF s.token # token THEN
        IF token = Scanner.TokIdent THEN
          Scanner.RaiseError(s, Base.ErrIdentExp)
        ELSIF token = Scanner.TokInt THEN
          Scanner.RaiseError(s, Base.ErrIntExp)
        ELSIF token = Scanner.TokChar THEN
          Scanner.RaiseError(s, Base.ErrCharExp)
        ELSE
          Scanner.RaiseError(s, token)
        END
      END
    END accept;


  PROCEDURE checkBool(VAR s : Scanner.Source; VAR g : Gen);

    BEGIN
      IF types[g.type].form # FormBool THEN
        Scanner.RaiseError(s, Base.ErrBoolExp)
      END
    END checkBool;


  PROCEDURE checkChar(VAR s : Scanner.Source; VAR g : Gen);

    BEGIN
      IF types[g.type].form # FormChar THEN
        Scanner.RaiseError(s, Base.ErrCharExp)
      END
    END checkChar;


  PROCEDURE checkInt(VAR s : Scanner.Source; VAR g : Gen);

    BEGIN
      IF types[g.type].form # FormInt THEN
        Scanner.RaiseError(s, Base.ErrIntExp)
      END
    END checkInt;


  PROCEDURE identDef(VAR s : Scanner.Source; VAR y : INTEGER;
                     class : INTEGER);

    BEGIN
      accept(s, Scanner.TokIdent);
      STableCopyIdent(symbols[Marker].name, s.ident);
      STableFindSymbol(y, topScope);
      IF symbols[y].next = Marker THEN
        STableAllocSymbol(s, class);
        symbols[y].next := allocSymbol;
        y := allocSymbol;
        Scanner.Next(s);
        IF s.token = Scanner.TokAsterisk THEN
          symbols[y].public := TRUE; Scanner.Next(s)
        END
      ELSE
        Scanner.RaiseError(s, Base.ErrMultipleDef)
      END
    END identDef;


  PROCEDURE identUse(VAR s : Scanner.Source; VAR y : INTEGER);

    VAR scope : INTEGER;


    PROCEDURE checkLevel(VAR s : Scanner.Source; VAR y : Symbol);

      BEGIN
        IF (y.level # 0) &
           (y.level # level) &
           (y.class = ClassVar) THEN
          Scanner.RaiseError(s, Base.ErrIdentUndef)
        END
      END checkLevel;


    PROCEDURE search(VAR s : Scanner.Source; VAR y : INTEGER;
                     scope : INTEGER; VAR ident : Scanner.Ident);

      BEGIN
        STableCopyIdent(symbols[Marker].name, ident);
        WHILE scope # -1 DO
          STableFindSymbol(y, scope);
          IF symbols[y].next # Marker THEN
            scope := -1
          ELSE
            scope := symbols[scope].scope
          END
        END;
        IF symbols[y].next # Marker THEN
          y := symbols[y].next
        ELSE
          Scanner.RaiseError(s, Base.ErrIdentUndef)
        END
      END search;


    BEGIN (* identUse *)
      search(s, y, topScope, s.ident);
      IF symbols[y].class = ClassQual THEN
        Scanner.Next(s); accept(s, Scanner.TokDot); 
        Scanner.Next(s); accept(s, Scanner.TokIdent); 
        search(s, y, symbols[y].value, s.ident)
      END;
      checkLevel(s, symbols[y])
    END identUse;


  PROCEDURE expr(VAR s : Scanner.Source; VAR g : Gen);

    VAR op : INTEGER; g2 : Gen;


    PROCEDURE Op1(VAR s : Scanner.Source; VAR g : Gen; op : INTEGER);

      BEGIN
        IF op = Scanner.TokMinus THEN
          IF g.mode = ClassConst THEN
            IF g.addr1 # Sys.MinInt THEN
              g.addr1 := -g.addr1
            ELSE
              Scanner.RaiseError(s, Base.ErrIntOverflow)
            END
          ELSE
            GenOp1(s, g, op)
          END
        END
      END Op1;


    PROCEDURE Op2(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen; op : INTEGER);


      PROCEDURE mul(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

        VAR isNeg : BOOLEAN; i : INTEGER;

        BEGIN
          IF (g.addr1 = 0) OR (g2.addr1 = 0) THEN
            g.addr1 := 0
          ELSE
            IF (g.addr1 > 0) & (g2.addr1 < 0) OR
               (g.addr1 < 0) & (g2.addr1 > 0) THEN
              isNeg := TRUE;
              i := Sys.MinInt
            ELSE
              isNeg := FALSE;
              i := Sys.MaxInt
            END;
            i := i DIV g2.addr1;
            IF (g.addr1 < 0) & (i > g.addr1) OR
               (g.addr1 > 0) & (i < g.addr1) THEN
              Scanner.RaiseError(s, Base.ErrIntOverflow)
            ELSE
              g.addr1 := g.addr1 * g2.addr1
            END
          END
        END mul;


      PROCEDURE div(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

        BEGIN
          IF g2.addr1 = 0 THEN
            Scanner.RaiseError(s, Base.ErrDivZero)
          ELSE
            g.addr1 := g.addr1 DIV g2.addr1
          END
        END div;


      PROCEDURE mod(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

        BEGIN
          IF g2.addr1 = 0 THEN
            Scanner.RaiseError(s, Base.ErrDivZero)
          ELSE
            g.addr1 := g.addr1 MOD g2.addr1
          END
        END mod;


      PROCEDURE add(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

        BEGIN
          IF Sys.MaxInt - g2.addr1 < g.addr1 THEN
            Scanner.RaiseError(s, Base.ErrIntOverflow)
          ELSE
            g.addr1 := g.addr1 + g2.addr1
          END
        END add;


      PROCEDURE sub(VAR s : Scanner.Source; VAR g : Gen; VAR g2 : Gen);

        BEGIN
          IF Sys.MinInt + g2.addr1 > g.addr1 THEN
            Scanner.RaiseError(s, Base.ErrIntOverflow)
          ELSE
            g.addr1 := g.addr1 - g2.addr1
          END
        END sub;


      BEGIN (* Op2 *)
        IF types[g.type].form = FormInt THEN
          IF (g.mode = ClassConst) & (g2.mode = ClassConst) THEN
            IF op = Scanner.TokAsterisk THEN mul(s, g, g2)
            ELSIF op = Scanner.TokDiv THEN div(s, g, g2)
            ELSIF op = Scanner.TokMod THEN mod(s, g, g2)
            ELSIF op = Scanner.TokPlus THEN add(s, g, g2)
            ELSIF op = Scanner.TokMinus THEN sub(s, g, g2)
            END
          ELSE
            GenOp2(s, g, g2, op)
          END
        ELSE
          GenOp2(s, g, g2, op)
        END
      END Op2;


    PROCEDURE simpleExpr(VAR s : Scanner.Source; VAR g : Gen);

      VAR op : INTEGER; g2 : Gen;


      PROCEDURE term(VAR s : Scanner.Source; VAR g : Gen);

        VAR op : INTEGER; g2 : Gen;


        PROCEDURE factor(VAR s : Scanner.Source; VAR g : Gen);

          VAR y : INTEGER;


          PROCEDURE selector(VAR s : Scanner.Source; VAR g : Gen);

            VAR g2 : Gen; y : INTEGER;

            BEGIN
              WHILE (s.token = Scanner.TokBrackL) OR
                    (s.token = Scanner.TokDot) DO
                IF s.token = Scanner.TokBrackL THEN
                  IF types[g.type].form = FormArray THEN
                    Scanner.Next(s);
                    g2.expConst := FALSE;
                    expr(s, g2);
                    checkInt(s, g2);
                    GenIndex(s, g, g2);
                    accept(s, Scanner.TokBrackR);
                    g.type := types[g.type].value;
                    g.size := types[g.type].size
                  ELSE
                    Scanner.RaiseError(s, Base.ErrArrayExp)
                  END
                ELSE
                  IF types[g.type].form = FormRecord THEN
                    Scanner.Next(s); accept(s, Scanner.TokIdent);
                    STableCopyIdent(symbols[Marker].name, s.ident);
                    STableFindSymbol(y, types[g.type].value);
                    y := symbols[y].next;
                    IF y # Marker THEN
                      g.type := symbols[y].type;
                      g.size := types[symbols[y].type].size;
                      GenField(g, symbols[y])
                    ELSE
                      Scanner.RaiseError(s, Base.ErrIdentUndef)
                    END
                  ELSE
                    Scanner.RaiseError(s, Base.ErrRecordExp)
                  END
                END;
                Scanner.Next(s)
              END
            END selector;


          PROCEDURE stdFunc(VAR s : Scanner.Source; VAR g : Gen; VAR y : Symbol);

            BEGIN
              Scanner.Next(s);
              accept(s, Scanner.TokParenL);
              Scanner.Next(s);
              g.expConst := FALSE;
              expr(s, g);
              IF y.value = 1 (* ADDR *) THEN
                GenAddr(s, g);
                g.type := intType
              ELSIF y.value = 2 (* CHR *) THEN
                checkInt(s, g);
                g.type := charType
              ELSIF y.value = 3 (* ORD *) THEN
                checkChar(s, g);
                g.type := intType
              END;
              accept(s, Scanner.TokParenR);
              Scanner.Next(s)
            END stdFunc;


          BEGIN (* factor *)
            IF s.token = Scanner.TokChar THEN
              g.mode := ClassConst;
              g.type := charType;
              g.addr1 := s.value;
              Scanner.Next(s)
            ELSIF s.token = Scanner.TokInt THEN
              g.mode := ClassConst;
              g.type := intType;
              g.addr1 := s.value;
              Scanner.Next(s)
            ELSIF s.token = Scanner.TokFalse THEN
              g.mode := ClassConst;
              g.type := boolType;
              g.addr1 := 0;
              Scanner.Next(s)
            ELSIF s.token = Scanner.TokTrue THEN
              g.mode := ClassConst;
              g.type := boolType;
              g.addr1 := 1;
              Scanner.Next(s)
            ELSIF s.token = Scanner.TokIdent THEN
              identUse(s, y);
              IF symbols[y].class = ClassFunc THEN
                stdFunc(s, g, symbols[y])
              ELSE
                GenMake(s, g, symbols[y]);
                Scanner.Next(s);
                selector(s, g)
              END
            ELSIF s.token = Scanner.TokParenL THEN
              Scanner.Next(s);
              expr(s, g);
              accept(s, Scanner.TokParenR);
              Scanner.Next(s)
            ELSIF s.token = Scanner.TokTilde THEN
              Scanner.Next(s);
              factor(s, g);
              checkBool(s, g);
              GenOp1(s, g, Scanner.TokTilde)
            ELSE
              Scanner.RaiseError(s, Base.ErrFactorExp)
            END
          END factor;


        BEGIN (* term *)
          factor(s, g);
          WHILE (s.token = Scanner.TokAsterisk) OR
                (s.token = Scanner.TokDiv) OR
                (s.token = Scanner.TokMod) OR
                (s.token = Scanner.TokAmper) DO
            op := s.token;
            IF op # Scanner.TokAmper THEN
              checkInt(s, g)
            ELSE
              checkBool(s, g);
              GenOp1(s, g, op)
            END;
            Scanner.Next(s);
            g2.expConst := FALSE;
            factor(s, g2);
            IF g.type = g2.type THEN
              Op2(s, g, g2, op)
            ELSE
              Scanner.RaiseError(s, Base.ErrIncompTypes)
            END
          END
        END term;


      BEGIN (* simpleExpr *)
        IF s.token = Scanner.TokPlus THEN
          Scanner.Next(s);
          term(s, g);
          checkInt(s, g)
        ELSIF s.token = Scanner.TokMinus THEN
          Scanner.Next(s);
          term(s, g);
          checkInt(s, g);
          Op1(s, g, Scanner.TokMinus)
        ELSE
          term(s, g)
        END;
        WHILE (s.token = Scanner.TokPlus) OR
              (s.token = Scanner.TokMinus) OR
              (s.token = Scanner.TokOr) DO
          op := s.token;
          IF op # Scanner.TokOr THEN
            checkInt(s, g)
          ELSE
            checkBool(s, g);
            GenOp1(s, g, op)
          END;
          Scanner.Next(s);
          g2.expConst := FALSE;
          term(s, g2);
          IF g.type = g2.type THEN
            Op2(s, g, g2, op)
          ELSE
            Scanner.RaiseError(s, Base.ErrIncompTypes)
          END
        END
      END simpleExpr;


    BEGIN (* expr *)
      simpleExpr(s, g);
      IF (s.token = Scanner.TokEq) OR (s.token = Scanner.TokEqN) OR
         (s.token = Scanner.TokLss) OR (s.token = Scanner.TokLEq) OR
         (s.token = Scanner.TokGtr) OR (s.token = Scanner.TokGEq) THEN
        IF ~g.expConst THEN
          op := s.token;
          Scanner.Next(s);
          g2.expConst := FALSE;
          simpleExpr(s, g2);
          IF g.type = g2.type THEN
            GenRel(s, g, g2, op);
            g.type := boolType;
            g.size := types[boolType].size
          ELSE
            Scanner.RaiseError(s, Base.ErrIncompTypes)
          END
        ELSE
          Scanner.RaiseError(s, Base.ErrConstExp)
        END
      END
    END expr;


  PROCEDURE constExpr(VAR s : Scanner.Source; VAR i : INTEGER);

    VAR g : Gen;

    BEGIN
      g.expConst := TRUE;
      expr(s, g);
      i := g.addr1
    END constExpr;


  PROCEDURE typeDef(VAR s : Scanner.Source; VAR y : INTEGER; VAR t : INTEGER;
                    class : INTEGER);

    VAR link : INTEGER; y2 : INTEGER;


    PROCEDURE array(VAR s : Scanner.Source; VAR t : INTEGER; link : INTEGER);

      VAR y : INTEGER;

      BEGIN
        Scanner.Next(s);
        STableAllocType(s, t, FormArray, 0, link);
        constExpr(s, types[t].len);
        accept(s, Scanner.TokOf);
        Scanner.Next(s);
        typeDef(s, y, types[t].value, 0);
        types[t].size := types[t].len * types[types[t].value].size
      END array;


    PROCEDURE record(VAR s : Scanner.Source; VAR t : INTEGER; link : INTEGER);

      VAR addr : INTEGER;


      PROCEDURE field(VAR s : Scanner.Source; VAR t : INTEGER; VAR addr : INTEGER);

        VAR y : INTEGER;

        BEGIN
          typeDef(s, y, t, ClassField);
          symbols[y].value := addr;
          addr := addr + types[t].size;
          GenAlign(addr)
        END field;


      BEGIN (* record *)
        addr := 0;
        STableAllocSymbol(s, ClassScope);
        Scanner.Next(s);
        field(s, t, addr);
        WHILE s.token = Scanner.TokSemic DO
          Scanner.Next(s); field(s, t, addr)
        END;
        accept(s, Scanner.TokEnd);
        STableAllocType(s, t, FormRecord, 0, link);
        types[t].size := addr;
        types[t].value := topScope;
        topScope := symbols[topScope].scope;
        Scanner.Next(s)
      END record;


    BEGIN (* typeDef *)
      link := -1;
      IF class # 0 THEN
        identDef(s, y, class);
        IF class = ClassType THEN
          link := y;
          accept(s, Scanner.TokEq)
        ELSE
          accept(s, Scanner.TokColon)
        END;
        Scanner.Next(s)
      END;
      IF s.token = Scanner.TokIdent THEN
        identUse(s, y2);
        IF (class # 0) & (y = y2) THEN
          Scanner.RaiseError(s, Base.ErrIdentUndef)
        END;
        t := symbols[y2].type;
        Scanner.Next(s)
      ELSIF s.token = Scanner.TokArray THEN
        array(s, t, link)
      ELSIF s.token = Scanner.TokRecord THEN
        record(s, t, link)
      ELSE
        Scanner.RaiseError(s, Base.ErrTypeExp)
      END;
      IF class # 0 THEN
        symbols[y].type := t
      END
    END typeDef;


  PROCEDURE stmts(VAR s : Scanner.Source);


    PROCEDURE stmt(VAR s : Scanner.Source);

      VAR y : INTEGER;


      PROCEDURE sys(VAR s : Scanner.Source);

        VAR call : INTEGER; g : Gen; g2 : Gen;

        BEGIN
          Scanner.Next(s); accept(s, Scanner.TokParenL);
          Scanner.Next(s); constExpr(s, call);
          accept(s, Scanner.TokComma); Scanner.Next(s);
          g.expConst := FALSE;
          expr(s, g);
          checkInt(s, g);
          IF g.mode = ClassVar THEN
            accept(s, Scanner.TokComma); Scanner.Next(s);
            g2.expConst := FALSE;
            expr(s, g2);
            checkInt(s, g2);
            GenSys(s, call, g, g2);
            accept(s, Scanner.TokParenR); Scanner.Next(s)
          ELSE
            Scanner.RaiseError(s, Base.ErrNotLValue)
          END
        END sys;


      PROCEDURE assign(VAR s : Scanner.Source);

        VAR g : Gen; g2 : Gen;

        BEGIN
          expr(s, g);
          IF g.mode = ClassVar THEN
            accept(s, Scanner.TokAssign); Scanner.Next(s);
            g2.expConst := FALSE;
            expr(s, g2);
            IF g.type = g2.type THEN
              GenStore(s, g, g2)
            ELSE
              Scanner.RaiseError(s, Base.ErrIncompTypes)
            END
          ELSE
            Scanner.RaiseError(s, Base.ErrNotLValue)
          END
        END assign;


      PROCEDURE call(VAR s : Scanner.Source; VAR proc : Symbol);

        VAR y : INTEGER;


        PROCEDURE param(VAR s : Scanner.Source; VAR y : INTEGER);

          VAR g : Gen;

          BEGIN
            g.expConst := FALSE;
            expr(s, g);
            IF (y # Marker) & (symbols[y].value > 0) THEN
              IF symbols[y].type = g.type THEN
                GenParam(s, g, symbols[y].value2 = 1)
              ELSE
                Scanner.RaiseError(s, Base.ErrIncompTypes)
              END
            ELSE
              Scanner.RaiseError(s, Base.ErrParamsTooMany)
            END;
            y := symbols[y].next
          END param;


        BEGIN (* call *)
          y := symbols[proc.scope].next;
          Scanner.Next(s);
          IF s.token = Scanner.TokParenL THEN
            Scanner.Next(s); param(s, y);
            WHILE s.token = Scanner.TokComma DO
              Scanner.Next(s); param(s, y)
            END;
            accept(s, Scanner.TokParenR); Scanner.Next(s)
          END;
          IF (y = Marker) OR (symbols[y].value < 0) THEN
            IF proc.value = Sys.MinInt THEN (* forward call *)
              GenFCall(proc.value2)
            ELSE (* backward call *)
              GenBCall(proc.value)
            END
          ELSE
            Scanner.RaiseError(s, Base.ErrParamsTooFew)
          END
        END call;


      PROCEDURE if(VAR s : Scanner.Source);

        VAR g : Gen; l : INTEGER;

        BEGIN
          Scanner.Next(s);
          g.expConst := FALSE;
          expr(s, g);
          checkBool(s, g);
          GenCondFJump(s, g);
          accept(s, Scanner.TokThen); Scanner.Next(s); stmts(s);
          l := 0;
          WHILE s.token = Scanner.TokElsif DO
            GenFJump(l);
            GenFixLink(g.addr1);
            Scanner.Next(s);
            expr(s, g);
            checkBool(s, g);
            GenCondFJump(s, g);
            accept(s, Scanner.TokThen); Scanner.Next(s); stmts(s)
          END;
          IF s.token = Scanner.TokElse THEN
            GenFJump(l);
            GenFixLink(g.addr1);
            Scanner.Next(s); stmts(s)
          ELSE
            GenFixLink(g.addr1)
          END;
          GenFixLink(l);
          accept(s, Scanner.TokEnd); Scanner.Next(s)
        END if;


      PROCEDURE while(VAR s : Scanner.Source);

        VAR g : Gen; l : INTEGER;

        BEGIN
          Scanner.Next(s);
          l := pc;
          g.expConst := FALSE;
          expr(s, g);
          checkBool(s, g);
          GenCondFJump(s, g);
          accept(s, Scanner.TokDo);
          Scanner.Next(s); stmts(s);
          GenBJump(l);
          GenFixLink(g.addr1);
          accept(s, Scanner.TokEnd); Scanner.Next(s)
        END while;


      BEGIN (* stmt *)
        IF s.token = Scanner.TokIdent THEN
          identUse(s, y);
          IF symbols[y].class = ClassProc THEN
            call(s, symbols[y])
          ELSE
            assign(s)
          END
        ELSIF s.token = Scanner.TokSys THEN
          sys(s)
        ELSIF s.token = Scanner.TokIf THEN
          if(s)
        ELSIF s.token = Scanner.TokWhile THEN
          while(s)
        ELSE
          Scanner.RaiseError(s, Base.ErrStmtExp)
        END;
        GenCheckRegs(s)
      END stmt;


    BEGIN (* stmts *)
      stmt(s);
      WHILE s.token = Scanner.TokSemic DO
        Scanner.Next(s); stmt(s)
      END
    END stmts;


  PROCEDURE decls(VAR s : Scanner.Source; VAR addr : INTEGER);


    PROCEDURE const(VAR s : Scanner.Source);

      VAR y : INTEGER;

      BEGIN
        WHILE s.token = Scanner.TokIdent DO
          identDef(s, y, ClassConst); accept(s, Scanner.TokEq);
          Scanner.Next(s); constExpr(s, symbols[y].value);
          symbols[y].type := intType;
          accept(s, Scanner.TokSemic);
          Scanner.Next(s)
        END
      END const;


    PROCEDURE type(VAR s : Scanner.Source);

      VAR y : INTEGER; t : INTEGER;

      BEGIN
        WHILE s.token = Scanner.TokIdent DO
          typeDef(s, y, t, ClassType);
          accept(s, Scanner.TokSemic); Scanner.Next(s)
        END
      END type;


    PROCEDURE var(VAR s : Scanner.Source; VAR addr : INTEGER);

      VAR y : INTEGER; t : INTEGER; size : INTEGER;

      BEGIN
        WHILE s.token = Scanner.TokIdent DO
          typeDef(s, y, t, ClassVar);
          size := types[t].size;
          GenAlign(size);
          addr := addr - size;
          symbols[y].value := addr;
          accept(s, Scanner.TokSemic); Scanner.Next(s)
        END
      END var;


    PROCEDURE proc(VAR s : Scanner.Source);

      VAR

        y : INTEGER; parsize : INTEGER; varsize : INTEGER;
        lastParam : INTEGER; eq : BOOLEAN;


      PROCEDURE params(VAR s : Scanner.Source; VAR addr : INTEGER);

        VAR y : INTEGER; t : INTEGER; offs : INTEGER;

        BEGIN
          IF s.token = Scanner.TokVar THEN
            Scanner.Next(s);
            typeDef(s, y, t, ClassVar);
            symbols[y].value2 := 1
          ELSE
            typeDef(s, y, t, ClassVar);
            symbols[y].value2 := 0
          END;
          IF symbols[y].value2 = 0 THEN
            IF types[t].form >= FormArray THEN
              Scanner.RaiseError(s, Base.ErrScalarExp)
            END
          END;
          addr := addr + 4; offs := addr;
          IF s.token = Scanner.TokSemic THEN
            Scanner.Next(s); params(s, addr)
          END;
          symbols[y].value := addr - offs + 8
        END params;


      BEGIN (* proc *)
        identDef(s, y, ClassProc);
        symbols[y].value := Sys.MinInt (* forward call marker *);
        symbols[y].value2 := 0;
        level := level + 1;
        STableAllocSymbol(s, ClassScope);
        symbols[y].scope := allocSymbol;
        parsize := 0;
        IF s.token = Scanner.TokParenL THEN
          Scanner.Next(s);
          IF s.token # Scanner.TokParenR THEN
            params(s, parsize); accept(s, Scanner.TokParenR); Scanner.Next(s)
          END
        END;
        accept(s, Scanner.TokSemic); Scanner.Next(s);
        lastParam := allocSymbol;
        varsize := 0;
        decls(s, varsize);
        IF symbols[y].value = Sys.MinInt THEN
          GenFixLink(-symbols[y].value2)
        END;
        symbols[y].value := -pc;
        GenEnter(varsize);
        accept(s, Scanner.TokBegin); Scanner.Next(s); stmts(s);
        accept(s, Scanner.TokEnd); Scanner.Next(s); accept(s, Scanner.TokIdent);
        STableCompIdent(eq, symbols[y].name, s.ident);
        IF eq THEN
          Scanner.Next(s); accept(s, Scanner.TokSemic); Scanner.Next(s)
        ELSE
          Scanner.RaiseError(s, Base.ErrNameMismatch)
        END;
        IF symbols[lastParam].next # Marker THEN
          STableDeallocScope(symbols[lastParam].next, FALSE);
          symbols[lastParam].next := Marker
        END;
        GenReturn(parsize);
        topScope := symbols[topScope].scope;
        level := level - 1
      END proc;


    BEGIN (* decls *)
      IF s.token = Scanner.TokConst THEN
        Scanner.Next(s); const(s)
      END;
      IF s.token = Scanner.TokType THEN
        Scanner.Next(s); type(s)
      END;
      IF s.token = Scanner.TokVar THEN
        Scanner.Next(s); var(s, addr)
      END;
      WHILE s.token = Scanner.TokProcedure DO
        Scanner.Next(s); proc(s)
      END
    END decls;


  PROCEDURE module(VAR s : Scanner.Source; VAR yModule : INTEGER);

    VAR eq : BOOLEAN;


    PROCEDURE imports(VAR s : Scanner.Source);


      PROCEDURE loadModule(VAR s : Scanner.Source; VAR yModule : INTEGER;
                           VAR name : Scanner.Ident);

        VAR
          sNew : Scanner.Source; i : INTEGER;
          fn : ARRAY Scanner.MaxLenIdent + 4 OF CHAR;


        PROCEDURE checkCyclcImport(VAR s : Scanner.Source; VAR yModule : INTEGER;
                                   VAR checkee : Scanner.Ident);

          VAR eq : BOOLEAN;

          BEGIN
            yModule := modules; eq := FALSE;
            WHILE (yModule # 0) & ~eq DO
              STableCompIdent(eq, symbols[yModule].name, checkee);
              IF ~eq THEN
                yModule := symbols[yModule].value2
              END
            END;
            IF eq & (symbols[yModule].value = 0) THEN
              Scanner.RaiseError(s, Base.ErrCyclicImports)
            END
          END checkCyclcImport;


        BEGIN (* loadModule *)
          checkCyclcImport(s, yModule, name);
          IF yModule = 0 THEN
            i := 0;
            WHILE i < name.len DO
              fn[i] := name.data[i]; i := i + 1
            END;
            fn[i] := 2EX; fn[i + 1] := 6EX; fn[i + 2] := 70X; fn[i + 3] := 0X;
            STableCopyIdent(sNew.name, name);
            Scanner.OpenSource(sNew, ADDR(fn));
            IF sNew.file = 0 THEN
              Scanner.RaiseError(s, Base.ErrOpenFile)
            END;
            module(sNew, yModule)
          END
        END loadModule;


      PROCEDURE import(VAR s : Scanner.Source);

        VAR y : INTEGER; yModule : INTEGER;

        BEGIN
          identDef(s, y, ClassQual);
          symbols[y].public := TRUE;
          IF s.token = Scanner.TokAssign THEN
            Scanner.Next(s); accept(s, Scanner.TokIdent);
            loadModule(s, symbols[y].value, s.ident);
            Scanner.Next(s)
          ELSE
            loadModule(s, symbols[y].value, symbols[y].name)
          END
        END import;


      BEGIN (* imports *)
        import(s);
        WHILE s.token = Scanner.TokComma DO
          Scanner.Next(s); import(s)
        END;
        accept(s, Scanner.TokSemic); Scanner.Next(s)
      END imports;


    BEGIN (* module *)
      Scanner.Next(s); accept(s, Scanner.TokModule);
      Scanner.Next(s); accept(s, Scanner.TokIdent);
      IF source.name.len = 0 THEN
        STableCopyIdent(s.name, s.ident)
      ELSE
        STableCompIdent(eq, s.name, s.ident);
        IF ~eq THEN
          Scanner.RaiseError(s, Base.ErrNameMismatch)
        END
      END;
      STableAllocSymbol(s, ClassScope);
      identDef(s, yModule, ClassModule);
      symbols[yModule].public := TRUE;
      symbols[yModule].value2 := modules;
      modules := yModule;
      accept(s, Scanner.TokSemic); Scanner.Next(s);
      IF s.token = Scanner.TokImport THEN
        Scanner.Next(s); imports(s)
      END;
      decls(s, varsize);
      entrypoint := pc;
      IF s.token = Scanner.TokBegin THEN
        IF symbols[yModule].value2 = 0 (* root module *) THEN
          Scanner.Next(s); stmts(s)
        ELSE
          Scanner.RaiseError(s, Base.ErrMainNotAllowed)
        END
      END;
      accept(s, Scanner.TokEnd); Scanner.Next(s); accept(s, Scanner.TokIdent);
      STableCompIdent(eq, symbols[yModule].name, s.ident);
      IF ~eq THEN
        Scanner.RaiseError(s, Base.ErrNameMismatch)
      END;
      Scanner.Next(s); accept(s, Scanner.TokDot);
      symbols[yModule].value := 1; (* compiled flag *)
      STableDeallocScope(topScope, symbols[yModule].value2 # 0);
      topScope := symbols[topScope].scope
    END module;


  BEGIN (* parse *)
    modules := 0; module(source, yModule)
  END parse;


BEGIN
  level := 0; pc := 0; varsize := 0;
  source.line := 0; source.pos := 0; source.name.len := 0;
  Scanner.OpenSource(source, 0 (* from args *));
  IF source.file = 0 THEN
    Scanner.RaiseError(source, Base.ErrOpenFile)
  END;
  Scanner.Initialize; STableInitialize;
  STableAllocSymbol(source, ClassScope);
  STableInitUniverse; GenInitRegs; parse;
  STableDeallocScope(topScope, FALSE) (* universe *);
  Scanner.CloseSource(source);
  GenEmitOc3(OcPOP, RegPC, RegSP, 0);
  GenEmit(varsize); GenEmit(entrypoint * 4);
  GenCreateFile
END NPC01.
